plugins {
    id "com.github.node-gradle.node" version "7.0.2"
}

node {
    version = "20.19.0"
    npmVersion = "10.2.4"
    download = true
}

tasks.register('buildAll') {
    group = 'build'
    description = 'Builds all subprojects'
    dependsOn subprojects.collect { it.tasks.named('build') }
}

def registerBuildGroupTask = { String taskName, String moduleKeyword ->
    tasks.register(taskName) {
        group = 'build'
        description = "Builds all ${moduleKeyword} subprojects"

        dependsOn(subprojects
                .findAll { it.name.contains(moduleKeyword) }
                .collect { it.tasks.named('build') }
        )
    }
}

tasks.register("seed", NpmTask) {
    group = "dev"
    dependsOn("installNodePackages")
    args = ["run", "seed"]
}

registerBuildGroupTask('buildBackend', 'backend')
registerBuildGroupTask('buildFrontend', 'frontend')

tasks.register("installNodePackages", NpmTask) {
    args = ["install"]
}

tasks.register("devStop", Exec) {
    group = "dev"
    commandLine "docker", "compose", "stop"
}

tasks.register("devStart", Exec) {
    group = "dev"
    commandLine "docker", "compose", "start"
}

tasks.register("devTearDown", Exec) {
    group = "dev"
    commandLine "docker", "compose", "down", "--rmi", "local", "-v"
}

tasks.register("dockerUp", Exec) {
    group = "dev"
    commandLine "docker", "compose", "up", "-d", "--build"
}

tasks.register("devUp") {
    group = "dev"
    dependsOn("buildBackend", "buildFrontend", "devTearDown", "dockerUp", "seed")
}