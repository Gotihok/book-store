tasks.register('buildAll') {
    group = 'build'
    description = 'Builds all subprojects'
    dependsOn subprojects.collect { it.tasks.named('build') }
}

def registerBuildGroupTask = { String taskName, String moduleKeyword ->
    tasks.register(taskName) {
        group = 'build'
        description = "Builds all ${moduleKeyword} subprojects"

        dependsOn(subprojects
                .findAll { it.name.contains(moduleKeyword) }
                .collect { it.tasks.named('build') }
        )
    }
}

tasks.register("seed", Exec) {
    group = "dev"
    description = "Seeds the database with predefined data using Node.js script"

    workingDir = projectDir

    // Run via shell to let PATH resolve npx/ts-node
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        commandLine "cmd", "/c", "npx ts-node scripts/seed/seed.ts"
    } else {
        commandLine "bash", "-c", "npx ts-node scripts/seed/seed.ts"
    }
}

registerBuildGroupTask('buildBackend', 'backend')
registerBuildGroupTask('buildFrontend', 'frontend')

tasks.register("devStop", Exec) {
    group = "dev"
    commandLine "docker", "compose", "stop"
}

tasks.register("devStart", Exec) {
    group = "dev"
    commandLine "docker", "compose", "start"
}

tasks.register("devTearDown", Exec) {
    group = "dev"
    commandLine "docker", "compose", "down", "--rmi", "local", "-v"
}

tasks.register("dockerUp", Exec) {
    group = "dev"
    commandLine "docker", "compose", "up", "-d", "--build"
}

tasks.register("devUp") {
    group = "dev"
    dependsOn("buildBackend", "buildFrontend", "devTearDown", "dockerUp", "seed")
}